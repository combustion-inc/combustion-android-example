plugins {
    id("com.android.application")
    id("kotlin-android")
    id("com.google.gms.google-services")
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--abbrev=4', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "inc.combustion.example"
        minSdk 24
        targetSdk 33
        versionCode 2003
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    viewBinding {
        enabled = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    // see: https://developer.android.com/studio/write/lint
    lint {
        // If set to true, turns off analysis progress reporting by lint.
        quiet true
        // If set to true (default), stops the build if errors are found.
        abortOnError true
        // If true, only report errors.
        ignoreWarnings false
        // If true, lint also checks all dependencies as part of its analysis. Recommended for
        // projects consisting of an app with library dependencies.
        checkDependencies true
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs += [
                    '-Xopt-in=kotlin.ExperimentalUnsignedTypes',
                    '-Xopt-in=com.juul.kable.ObsoleteKableApi',
                    '-Xjvm-default=compatibility'
            ]
        }
    }
}

dependencies {
    try {
        var localAndroidBle = file(getProperty("combustion.use-local-framework"))
        if(localAndroidBle.exists()) {
            implementation(project(":combustion-android-ble"))
        } else {
            implementation("com.github.combustion-inc:combustion-android-ble:$combustion_framework_version")
        }
    } catch (e) {
        implementation("com.github.combustion-inc:combustion-android-ble:$combustion_framework_version")
    }
    implementation('androidx.core:core-ktx:1.9.0')
    implementation("androidx.appcompat:appcompat:1.6.1")
    implementation("com.google.android.material:material:1.8.0")
    implementation("androidx.compose.ui:ui:1.3.3")
    implementation("androidx.compose.material:material:1.3.1")
    implementation("androidx.compose.ui:ui-tooling-preview:1.3.3")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_runtime_ktx_version")
    implementation("androidx.activity:activity-compose:$activity_compose_version")
    implementation("androidx.navigation:navigation-compose:$navigation_compose_version")
    implementation("androidx.navigation:navigation-common-ktx:$navigation_common_ktx_version")
    implementation("com.github.alorma:compose-settings-ui:$settings_compose_version")
    implementation("com.juul.kable:core:$kable_core_version")
    implementation("com.vmadalin:easypermissions-ktx:$easypermissions_ktx_version")
    implementation("androidx.lifecycle:lifecycle-service:$lifecycle_service_version")
    implementation("org.jetbrains.kotlin:kotlin-reflect:1.7.10")
    implementation("androidx.compose.ui:ui-viewbinding:1.3.3")
    implementation("com.github.PhilJay:MPAndroidChart:$mpandroidchart_version")
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.0.2")
    androidTestImplementation("androidx.test.ext:junit:1.1.5")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.5.1")
    androidTestImplementation("androidx.compose.ui:ui-test-junit4:1.3.3")
    debugImplementation("androidx.compose.ui:ui-tooling:1.3.3")
    testImplementation('junit:junit:4.13.2')
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version")
}
